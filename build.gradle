
buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }

    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

ext.configFile = file "build.properties"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = config.version
group = config.mod_id
archivesBaseName = "EnhancedPortals_${project.config.mc_version}"

minecraft {
    version = "1.9.4-12.17.0.1976"
    runDir = "idea"
    mappings = project.config.mappings_version
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        expand ([
            'mod_version': version,
            'mc_version': project.config.mc_version,
            'mod_id': project.config.mod_id
        ])
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

repositories {
    maven {
        name = "Sangar OC"
        url = "http://maven.cil.li/"
    }

    maven {
        name = "TechNut GuideAPI"
        url = 'http://tehnut.info/maven/'
    }
}

dependencies {
    compile "li.cil.oc:OpenComputers:MC1.9.4-${project.config.oc_version}.+:api"
    compile "info.amerifrance.guideapi:Guide-API:${project.config.mc_version}-${project.config.guideapi_version}"
    compile "net.darkhax.tesla:Tesla:1.9.4-${project.config.tesla_version}"
}

jar {
    appendix = 'universal'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    appendix = 'deobf'
}

artifacts {
    archives deobfJar
}

idea {
    module {
        // For some reason this is necessary for IDEA 2014 workspaces to recognize assets
        inheritOutputDirs = true
    }
}
